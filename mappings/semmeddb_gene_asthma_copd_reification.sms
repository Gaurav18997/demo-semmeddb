PREFIX sdb: <https://skr3.nlm.nih.gov/SemMedDB/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# stardog-admin virtual import semmeddb --format SMS2 mappings/semmeddb.properties mappings/semmeddb_gene_asthma_copd_reification.sms 

MAPPING :SemmedDB_Predications
FROM SQL {
  	SELECT
		predication_id,
		subject_cui, 
		subject_name, 
		subject_semtype,
		LOWER(predicate) as lc_predicate, 
		object_cui, 
		object_name, 
		object_semtype
	FROM predication 
	WHERE subject_semtype = 'gngm'  AND ( object_name LIKE 'COPD%' OR object_name = 'Asthma' )
}
TO {

	?pid rdf:type sdb:Predication ;
		 rdf:subject ?s ;
		 rdf:predicate ?p ;
		 rdf:object ?o	.
	
	?s 	rdf:type ?st ;
		rdfs:label ?subject_name ; # multilingual (English, Latin)
		sdb:cui ?subject_cui ;
		?p ?o . # assertion of above reified statement
		
	?o	rdf:type ?ot ;
		rdfs:label ?object_name; 
		sdb:cui ?object_cui .

} 
WHERE {
	BIND(template("https://skr3.nlm.nih.gov/SemMedDB/{predication_id}") AS ?pid)
	BIND(template("https://skr3.nlm.nih.gov/SemMedDB/{subject_cui}") AS ?s)
	BIND(template("https://skr3.nlm.nih.gov/SemMedDB/{subject_semtype}") AS ?st)
	BIND(template("https://skr3.nlm.nih.gov/SemMedDB/{lc_predicate}") AS ?p)
	BIND(template("https://skr3.nlm.nih.gov/SemMedDB/{object_cui}") AS ?o)
	BIND(template("https://skr3.nlm.nih.gov/SemMedDB/{object_semtype}") AS ?ot)
}
